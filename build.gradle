plugins {
	id("fabric-loom") version "1.10-SNAPSHOT"
	id("maven-publish")
	id 'org.jetbrains.kotlin.jvm'
}

version = project.mod_version
group = project.maven_group

base {
	archivesName = project.archives_base_name
}

repositories {
	// Add repositories to retrieve artifacts from in here.
	// You should only use this when depending on other mods because
	// Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
	// See https://docs.gradle.org/current/userguide/declaring_repositories.html
	// for more information about repositories.

	mavenCentral()
	gradlePluginPortal()
	maven { url = uri("https://jitpack.io") }
	maven { url = uri("https://maven.blamejared.com") }
	maven { url = uri("https://maven.fabricmc.net/") }
	maven { url = uri("https://maven.ladysnake.org/releases") } // Cardinal Components
	maven { url = uri("https://maven.parchmentmc.org") }
	maven { url = uri("https://maven.shedaniel.me") }
	maven { url = uri("https://maven.terraformersmc.com/releases") }

	// add the modrinth maven!
	exclusiveContent {
		forRepository {
			maven {
				name = "Modrinth"
				url = "https://api.modrinth.com/maven"
			}
		}
		filter {includeGroup "maven.modrinth"}
	}
	exclusiveContent {
		filter {includeGroup("libs")}
		forRepository { flatDir{dir(rootProject.file("libs"))} }
	}
}

loom {
	splitEnvironmentSourceSets()

	mods {
		"movesthemind" {
			sourceSet(sourceSets.main)
			sourceSet(sourceSets.client)
		}
	}

}

fabricApi { configureDataGeneration {client = true} }

dependencies {
	minecraft("com.mojang:minecraft:${project.minecraft_version}")
	mappings(loom.officialMojangMappings())

	modApi(libs.fabric.api)
	modImplementation(libs.fabric.loader)

	modApi(libs.hexcasting.fabric) {exclude(module: "phosphor")} // If not excluded here, calls a nonexistent method and crashes the dev client
	// Hex Casting dependencies
	// we use modLocalRuntime to add these to the development runtime, but not at compile time or for consumers of this project
	modLocalRuntime(libs.paucal.fabric)
	modLocalRuntime(libs.patchouli.fabric)
	modLocalRuntime(libs.cardinalComponents)
	modLocalRuntime(libs.serializationHooks)
	modLocalRuntime(libs.trinkets)
	modLocalRuntime(libs.inline.fabric) { transitive = false }
	modApi(libs.clothConfig.fabric) {exclude(group: "net.fabricmc.fabric-api") }
	modImplementation(libs.modMenu)

	modImplementation(libs.kotlin.fabric)
	implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
	// need this for config flag forcing, I think
	modImplementation("vazkii.patchouli:Patchouli:1.20.1-84-FABRIC")
}

processResources {
	inputs.property("version", project.version)
	filesMatching("fabric.mod.json") {expand("version": inputs.properties.version)}
}

tasks.withType(JavaCompile).configureEach {it.options.release = 17}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()

}

jar {
	inputs.property("archivesName", project.base.archivesName)

	from("LICENSE") {
		rename({ "${it}_${inputs.properties.archivesName}"})
	}
}

// configure the maven publication
publishing {
	publications {
		create("mavenJava", MavenPublication) {
			artifactId = project.archives_base_name
			from(components.java)
		}
	}

	// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	repositories {
		// Add repositories to publish to here.
		// Notice: This block does NOT have the same function as the block in the top level.
		// The repositories here will be used for publishing your artifact, not for
		// retrieving dependencies.
	}
}
kotlin {
	jvmToolchain(17)
}